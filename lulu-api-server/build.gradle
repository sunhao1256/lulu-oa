plugins {
    id 'java'
    id "org.springframework.boot"
}
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
dependencies {
    implementation platform(project(':lulu-bom'))

    implementation project(':lulu-common')
    annotationProcessor project(':lulu-common')

    implementation project(':lulu-auth')
    implementation project(':lulu-bpmn')

    implementation 'com.google.guava:guava'
    runtimeOnly 'mysql:mysql-connector-java'


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

def host = "registry.cn-hangzhou.aliyuncs.com/sunhao1256"
tasks.named('bootBuildImage') {
    imageName = "${host}/${project.name}"
    builder = "paketobuildpacks/builder:0.3.215-base"
    pullPolicy= "IF_NOT_PRESENT"
    //jre 11.0.18
    environment["BP_JVM_VERSION"] = "11"
    environment["BP_ENABLE_RUNTIME_CERT_BINDING"] = 'false'
    environment["BPE_OVERRIDE_BPL_JVM_THREAD_COUNT"] = "30"
    environment["BPE_DELIM_JAVA_TOOL_OPTIONS"] = " "


//    diminish memory usage of jvm
    environment["BPE_APPEND_JAVA_TOOL_OPTIONS"] = '''
-XX:+HeapDumpOnOutOfMemoryError
-XX:ReservedCodeCacheSize=16M -XX:MaxDirectMemorySize=16M 
-Dspring.profiles.active=pro
-Xmx100M
'''
    buildCache {
        volume {
            name = "cache-${rootProject.name}.build"
        }
    }
    publish = true
    docker {
        publishRegistry {
            username = "$System.env.RegistryUser"
            password = "$System.env.RegistryPass"
        }
    }
    launchCache {
        volume {
            name = "cache-${rootProject.name}.launch"
        }
    }
    bindings = [
            "${rootDir}/bindings/bellsoft-jdk-config:/platform/bindings/bellsoft-jdk-config"
    ]
}
